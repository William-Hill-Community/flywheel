apply plugin: 'java'

group = project(':').group
version = project(':').version

def packageName = 'flywheel-wha'

repositories {
  jcenter()
}

dependencies {
  compile project(':flywheel-standalone')
  
  runtime project(':flywheel-backplane-kafka')
  runtime project(':flywheel-logging-splunk')
}

task copyStandaloneScripts(type: Copy) {
  from '../standalone/scripts'
  into 'build/scripts'
}

jar {
  dependsOn configurations.compile
  dependsOn configurations.runtime
  from sourceSets.main.output
  from { // bundle all dependencies
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
  }   
  baseName packageName + '-full'
  finalizedBy copyStandaloneScripts
}

task dockerBuild(type: Exec) {
  dependsOn ':flywheel-wha:jar'
  def dockerRepo = System.getProperty('flywheel.wha.repo', 'flywheel-wha')
  commandLine 'sh', '-c', "docker build -t $dockerRepo ."
  doLast {
    println "Built image $dockerRepo"
  }
}