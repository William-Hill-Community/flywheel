def packageName = 'flywheel-logging-splunk'
version = project(':').version

dependencies {
  compile project(':')
  compile 'org.slf4j:slf4j-log4j12:1.7.25'
  compile 'log4j:log4j:1.2.17' // need this on the compile classpath for the JSON layout and Splunk appender
  
  // used by Splunk HEC logger
  compile 'org.apache.httpcomponents:httpclient:4.5.3'
  compile 'org.apache.httpcomponents:httpasyncclient:4.1.3'
}

jar {
  baseName packageName
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = 'javadoc'
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = 'sources'
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(':').group
      artifactId packageName
      version project(':').version
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def envUser = 'BINTRAY_USER'
def envKey = 'BINTRAY_KEY'
def packageDesc = 'Splunk Log4j appender with JSON layout for Flywheel'
def repoName = 'flywheel'
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ['mavenJava']
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = 'william-hill-community'
    desc = packageDesc
    websiteUrl = "https://github.com/william-hill-community/${repoName}"
    licenses = ['BSD New']
    vcsUrl = "https://github.com/william-hill-community/${repoName}"
    issueTrackerUrl = "https://github.com/william-hill-community/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "william-hill-community/${repoName}"
    override = true
    publish = false
    version {
      name = project(':').version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(':').version
    }
  }
}
